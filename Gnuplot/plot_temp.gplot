set terminal pngcairo enhanced font 'Arial,12' size 1000,600

set style line 1 lc rgb '#1f77b4' lt 1 lw 2 pt 7 ps 1.5
set style line 2 lc rgb '#ff7f0e' lt 1 lw 2 pt 7 ps 1.5
set style line 3 lc rgb '#2ca02c' lt 1 lw 2 pt 7 ps 1.5
set style line 4 lc rgb '#d62728' lt 1 lw 2 pt 7 ps 1.5
set style line 5 lc rgb '#9467bd' lt 1 lw 2 pt 7 ps 1.5
set style line 6 lc rgb '#8c564b' lt 1 lw 2 pt 7 ps 1.5

do for [t=0:20] {
    temp_filename = sprintf("data_temp/temperature_t%d.dat", t)
    rho_filename = sprintf("data_temp/rho_t%d.dat", t)

    set title sprintf("Température et Masses Volumiques, t = %d s", t*100) font ",16"
    set xlabel "x (m)" font ",14"
    set ylabel "Température (K)" font ",14"
    set ytics nomirror

    set y2label "Masse Volumique (kg/m^3)" font ",14"
    set y2tics

    set grid

    output_file = sprintf("img_temp/temperature_rho_t%d.png", t)
    set output output_file

    plot temp_filename using 1:2 with lines linestyle 1 title "Température" axes x1y1, \
         rho_filename using 1:2 with lines linestyle 2 title "rho_b" axes x1y2, \
         rho_filename using 1:3 with lines linestyle 3 title "rho_c" axes x1y2, \
         rho_filename using 1:4 with lines linestyle 4 title "rho_g" axes x1y2, \
         rho_filename using 1:5 with lines linestyle 5 title "rho_l" axes x1y2, \
         rho_filename using 1:6 with lines linestyle 6 title "rho_v" axes x1y2
}

set output