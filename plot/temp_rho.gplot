reset
set terminal pngcairo size 1800,1200 font "Helvetica,28" enhanced
set output

t_values = ""
do for [i=0:200:5] {
    t_values = t_values.sprintf(" %.1f", i)
}

schema_values = "EE EI CK"

schema = 3
ci = 1
cl = 1
L = 0.2
imax = 128

# Palette Plasma
set palette defined (0 "#0d0887", 1 "#46039f", 2 "#7201a8", 3 "#9c179e", 4 "#bd3786", 5 "#d8576b", 6 "#ed7953", 7 "#fb9f3a", 8 "#fdca26", 9 "#f0f921")

set xlabel "x" font "Helvetica Bold,24" offset 0, 0
set ylabel "{/Symbol r} (kg/m^3)" font "Helvetica Bold,24" offset 1,0
set grid lw 1.5
set tics font "Helvetica,18"
set bmargin 5
set key outside bottom horizontal spacing  1.2 font "Helvetica,22" box 
set colorbox

set cbrange[300:800]
set cblabel "T (K)" font "Helvetica Bold,24"
set style line 1 lt 1 lc rgb "#b8915d" lw 2 pt 6 ps 1.4 # rho_b
set style line 2 lt 1 lc rgb "#292929" lw 2 pt 6 ps 1.4 # rho_c
set style line 3 lt 1 lc rgb "#2ca02c" lw 2 pt 6 ps 1.4 # rho_g
set style line 4 lt 1 lc rgb "#45a7e0" lw 2 pt 6 ps 1.4 # rho_l
set style line 5 lt 1 lc rgb "#a772d8" lw 2 pt 6 ps 1.4 # rho_v

do for [i=1:words(t_values)] {
    t = word(t_values, i)
    fichier = sprintf("data/1D/temp_%s_L0.2_bois1_tn%s_imax128.dat", word(schema_values, schema), t)

    set title sprintf("Solution Num√©rique 1D (%s), t = %s", word(schema_values, schema), t)

    set output sprintf("images/1D/temp_%02d.png", i)

    plot fichier u 1:2:2 w l palette lw 6 title "T_i^n", \
         '' u 1:3 w lp ls 1 title "rho_b", \
         '' u 1:4 w lp ls 2 title "rho_c", \
         '' u 1:5 w lp ls 3 title "rho_g", \
         '' u 1:6 w lp ls 4 title "rho_l", \
         '' u 1:7 w lp ls 5 title "rho_v"
}

#convert -delay 20 -loop 0 images/1D/temp_*.png temp_rho.gif
unset output