reset
set terminal pngcairo size 3600,1600 font "Helvetica,28" enhanced
set output

# Paramètres initiaux
t_values = ""
do for [i=0:40] {
    t_values = t_values.sprintf(" %.1f", i*5)
}

schema_values = "EE EI CK"
schema = 2
ci = 1
tb1 = 1
global_Lx = 0.2
global_Ly = 0.2
nx = 128
ny = 128

do for [i=1:words(t_values)] {
    t = word(t_values, i)

    Lx = global_Lx
    Ly = global_Ly

    fichier_temp = sprintf("data/2D/temp_%s_Lx%.1f_Ly%.1f_bois%d_tn%s_nx%d.dat", word(schema_values, schema), Lx, Ly, ci, t, nx)
    fichier_rho  = sprintf("data/2D/temp_%s_Lx%.1f_Ly%.1f_bois%d_tn%s_nx%d.dat", word(schema_values, schema), Lx, Ly, tb1, t, nx)

    set output sprintf("images/2D/multiplot_%02d.png", i)
    set multiplot layout 1,2 title sprintf("Schéma d'Euler implicite, t = %s s", t) font "Helvetica Bold,28"

    # PLOT TEMP
    reset
    Lx = global_Lx
    Ly = global_Ly
    fichier_temp = sprintf("data/2D/temp_%s_Lx%.1f_Ly%.1f_bois%d_tn%s_nx%d.dat", word(schema_values, schema), Lx, Ly, ci, t, nx)

    set view map
    set size ratio -1
    unset border
    set tics scale 1.5
    set xtics font "Helvetica,20" offset 0,0.5
    set ytics font "Helvetica,20" offset 0.5,0
    set xlabel "x (m)" font "Helvetica Bold,24" offset 0,1
    set ylabel "y (m)" font "Helvetica Bold,24" offset 1,0
    set xrange[0:Lx]
    set yrange[0:Ly]
    set palette defined (0 "#0d0887", 1 "#46039f", 2 "#7201a8", 3 "#9c179e", 4 "#bd3786", 5 "#d8576b", 6 "#ed7953", 7 "#fb9f3a", 8 "#fdca26", 9 "#f0f921")
    set cbrange[300:1000]
    set cblabel "T_{i,j} (K)" font "Helvetica Bold,24"
    set title "Température 2D" font "Helvetica Bold,24"
    plot fichier_temp using 1:2:3 with image notitle

    # PLOT RHO
    reset
    Lx = global_Lx
    Ly = global_Ly
    fichier_rho = sprintf("data/2D/temp_%s_Lx%.1f_Ly%.1f_bois%d_tn%s_nx%d.dat", word(schema_values, schema), Lx, Ly, tb1, t, nx)

    set view map
    set size ratio -1
    unset border
    set tics scale 1.5
    set xtics font "Helvetica,20" offset 0,0.5
    set ytics font "Helvetica,20" offset 0.5,0
    set xlabel "x (m)" font "Helvetica Bold,24" offset 0,1
    set ylabel "y (m)" font "Helvetica Bold,24" offset 1,0
    set xrange[0:Lx]
    set yrange[0:Ly]
    set palette defined (0 "#e6e6e6", 1 "black")
    set cbrange[0:700]
    set cblabel "{/Symbol r}_{c} (kg/m^3)" font "Helvetica Bold,24"
    set title "Masse volumique charbon 2D" font "Helvetica Bold,24"
    plot fichier_rho using 1:2:5 with image notitle

    unset multiplot
}

unset output
