reset
set terminal pngcairo size 1800,1600 font "Helvetica,28" enhanced
set output

t_values = ""
do for [i=0:40]{
    t_values = t_values.sprintf(" %.1f", i*5)
}

schema_values = "EE EI CK"
schema = 2
tb1 = 1
tb2 = 5
Lx = 0.2
Ly = 0.2
nx = 128
ny = 128

set view map
set size ratio -1
unset border
set tics scale 1.5
set xtics font "Helvetica,20" offset 0,0.5
set ytics font "Helvetica,20" offset 0.5,0

set xlabel "x (m)" font "Helvetica Bold,24" offset 0,1
set ylabel "y (m)" font "Helvetica Bold,24" offset 1,0

set palette defined (0 "#e6e6e6", 1 "black")

set cbrange[0:700]
set yrange[0:0.20]
set xrange[0:0.20]

set cblabel "{/Symbol r}_{c} (kg/m^3)" font "Helvetica Bold,24"

do for [i=1:words(t_values)] {
    t = word(t_values, i)
    fichier = sprintf("data/2D/temp_%s_Lx%.1f_Ly%.1f_bois%d_tn%s_nx%d.dat", word(schema_values, schema), Lx, Ly, tb1, t, nx)

    set title sprintf("Masse volumique charbon 2D - Sch√©ma %s, t = %s s", word(schema_values, schema), t) font "Helvetica Bold,24"
    set output sprintf("images/2D/rho_%02d.png", i)

    plot fichier using 1:2:5 with image notitle
}

#convert -delay 20 -loop 0 images/2D/rho_*.png rho_2D.gif
unset output
