set terminal wxt size 1400,900
set key font "Helvetica Bold,20"
set tics font "Helvetica Bold,18"
set xlabel "ln({/Symbol D}t)" font "Helvetica Bold,20" offset 0, 0
set ylabel "ln(E)" rotate by 0 font "Helvetica Bold,20" offset 2, 0

set key left box

set logscale xy 
set format x "10^{%L}"
set format y "10^{%L}"
set xtics format "10^{%L}"
set ytics format "10^{%L}"
set grid lw 1.5
mc1(x) = a1*x+b1
fit mc1(x) "data/err/EItemp.dat" using (log($1)):(log($2)) via a1,b1

mc2(x) = a2*x+b2
fit mc2(x) "data/err/CKtemp.dat" using (log($1)):(log($2)) via a2,b2

plot    "data/err/EItemp.dat" u 1:2 w p lw 3 pt 6 ps 2 lc "red" title "Erreur globale (EI)", \
        "data/err/CKtemp.dat" u 1:2 w p lw 3 pt 6 ps 2 lc "blue" title "Erreur globale (CK)", \
        exp(mc1(log(x))) title sprintf("Reg. linéaire d'ordre %.3f", a1) lt 2 dt 2 lw 3 lc "red", \
        exp(mc2(log(x))) title sprintf("Reg. linéaire d'ordre %.3f", a2) lt 2 dt 2 lw 3 lc "blue"


pause -1